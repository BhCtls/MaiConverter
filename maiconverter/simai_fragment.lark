// Do not pass empty strings or "E"

// TODO: There are still some edge cases that this lark file doesn't
// cover. Mostly stuff that falls under "Special format" in 
// https://w.atwiki.jp/simai/pages/25.html
// But I hate the simai format and implementing these are as fun as
// playing Pandora Paradoxxx Re:master.

// TODO: Can we make this LALR compatible? We can get a large 
// performance increase from this.

?start: chain

?value: slide_note
      | hold_note
      | tap_note
      | divisor
      | touch_tap_note
      | touch_hold_note
      | bpm

// In simai, the "/" can be ommited for tap notes. Fun!
chain: value ("/"? value)*

NUMBER: DECIMAL | INT

button: "1".."8"
touch_location: /C[12]?/ | /B[1-8]/ | /E[1-8]/
duration: "[" equivalent_bpm INT ":" INT "]"
// For slides with delay that's not 0.25
equivalent_bpm: (NUMBER "#")?
slide_pattern_literal: /-/ | /\^/ | /</ | />/ | /p{1,2}/ | /q{1,2}/ | /s/ | /z/ | /v/ | /w/
slide_pattern_v: /V/ button
slide_pattern: slide_pattern_literal | slide_pattern_v

bpm: "(" NUMBER ")"
divisor: "{" INT "}"

tap_modifier: (/b/ | /x/)? (/\$/)?
hold_modifier: (/x/)?
slide_modifier: (/b/ | /x/ | /\?/ | /\$/ | /!/ )?
touch_modifier: (/f/)?

tap_note: button tap_modifier
hold_note: button "h" hold_modifier duration
slide_note: button slide_modifier slide_pattern button duration chained_slides
chained_slides: chained_slide_note*
chained_slide_note: "*" slide_pattern button duration
touch_tap_note: touch_location touch_modifier
touch_hold_note: touch_location "h" touch_modifier duration

%import common.INT
%import common.DECIMAL
%import common.WS
%ignore WS
