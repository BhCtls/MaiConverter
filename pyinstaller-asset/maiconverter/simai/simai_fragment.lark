// Do not pass empty strings or "E"

// TODO: There are still some edge cases that this lark file doesn't
// cover. Mostly stuff that falls under "Special format" in 
// https://w.atwiki.jp/simai/pages/25.html
// But I hate the simai format and implementing these are as fun as
// playing Pandora Paradoxxx Re:master.

?start: chain

?value: slide_note
      | tap_hold_note
      | divisor
      | touch_tap_hold_note
      | bpm
      | pseudo_each

chain: value ("/"? value)*

duration: "[" /(\d+(\.\d*)?#)/? INT ":" INT "]"
// duration: "[" equivalent_bpm INT ":" INT "]"

bpm: "(" NUMBER ")"
divisor: "{" NUMBER "}"




slide_connector: /[-^<>szvw]|p{1,2}|q{1,2}|V[0-8]/
slide_modifier: /[bx$]+/
slide_pos: /[0-8]/

slide_beg: (slide_connector slide_pos slide_modifier? duration? slide_modifier?)* slide_connector slide_pos slide_modifier? duration slide_modifier?
chained_slide_note: "*" slide_modifier? slide_beg
slide_note: slide_pos slide_modifier? slide_beg chained_slide_note*

tap_hold_note: /[0-8][hbex$]*/ duration?


touch_tap_hold_note: /((C[012]?)|(B[0-8])|(E[0-8])|(A[0-8])|(D[0-8]))[hfe]*/ duration?

pseudo_each: "`" (slide_note | tap_hold_note | touch_tap_hold_note)

%import common.INT
%import common.DECIMAL
%import common.NUMBER
%import common.WS
%ignore WS